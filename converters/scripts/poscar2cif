#!/usr/bin/perl

use strict;
use warnings;
use COD::Cell qw( vectors2cell );
use COD::CIF::Tags::Print qw( print_single_tag_and_value );
use COD::Formulae::Print qw( sprint_formula );

for my $filename (@ARGV) {
    open( my $inp, $filename );
    my @poscar_raw = <$inp>;
    close( $inp );

    my @poscar;
    foreach( @poscar_raw ) {
        my $line = $_;
        $line =~ s/^\s+//;
        $line =~ s/!.*$//;
        $line =~ s/\s+$//;
        push @poscar, $line;
    }

    my $label  = shift @poscar;
    my $factor = shift @poscar;

    my @vectors;
    for my $i (0..2) {
        push( @vectors,
              [ map { $_ * $factor }
                map { split /\s+/ }
                    shift @poscar ] );
    }

    my( $a, $b, $c, $alpha, $beta, $gamma ) = vectors2cell( @vectors );

    my $dataname = ( split /\s+/, $label )[0];

    print "data_$dataname\n";

    print_single_tag_and_value( '_cell_length_a', $a );
    print_single_tag_and_value( '_cell_length_b', $b );
    print_single_tag_and_value( '_cell_length_c', $c );
    print_single_tag_and_value( '_cell_angle_alpha', $alpha );
    print_single_tag_and_value( '_cell_angle_beta',  $beta );
    print_single_tag_and_value( '_cell_angle_gamma', $gamma );

    my $natoms = [ split /\s+/, shift @poscar ];
    my $types;
    if( $natoms && $natoms->[0] !~ /^\d+$/ ) {
        # Special case when both atom types and numbers are given.
        # Not sure if it's regular in POSCAR, but can be found at least in
        # the results of Huan et al. (2015).
        $types = $natoms;
        $natoms = [ split /\s+/, shift @poscar ];
    }

    if( $types ) {
        my %formula_hash;
        @formula_hash{@$types} = @$natoms;

        print_single_tag_and_value( '_chemical_formula_sum',
                                    sprint_formula( \%formula_hash ) );
    }

    local $\ = "\n";
    local $, = "\n";

    my $coord_type = lc shift @poscar;

    # As per http://cms.mpi.univie.ac.at/vasp/guide/node59.html:
    $coord_type = 'cartesian' if $coord_type =~ /^[ck]/;

    die "$0: $filename: unknown coordinate conversion type: $coord_type"
        if $coord_type ne 'direct' && $coord_type ne 'cartesian';

    print 'loop_';
    print '_atom_site_label' if $types;
    if( $coord_type eq 'direct' ) {
        print '_atom_site_fract_x',
              '_atom_site_fract_y',
              '_atom_site_fract_z';
    } else {
        print '_atom_site_Cartn_x',
              '_atom_site_Cartn_y',
              '_atom_site_Cartn_z';
    }

    local $, = ' ';

    my $type = 0;
    my $i = 0;
    foreach( @poscar ) {
        if( $types ) {
            local $\ = ' ';
            print $types->[$type] . ($i + 1);
            $i++;
            if( $i >= $natoms->[$type] ) {
                $type++;
                $i = 0;
            }
        }

        my @coord = split /\s+/, $_;
        if( $coord_type eq 'cartesian' ) {
            @coord = map { $_ * $factor } @coord;
        }
        print @coord;
    }

    undef $,;
    undef $\;
    $poscar_raw[-1] =~ s/\n$//;
    print_single_tag_and_value( '_tcod_computation_log_file',
                                join( '', @poscar_raw ) );
}
