#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Extract data from Abinit (http://www.abinit.org/) log files and
#  print them out in CIF (http://www.iucr.org/resources/cif) format
#  using TCOD (http://www.crystallography.net/tcod/) dictionaries
#  (http://www.crystallography.net/tcod/cif/dictionaries/)
#**

use strict;
use AtomProperties;
use File::Basename;
use SOptions;
use SUsage;

my $Id = '$Id$';

my $input_file_list_name;
my $input_task_description_name;

my $atoms = \%AtomProperties::atoms;

my %periodic_table =
    map {
        $atoms->{$_}{atomic_number}, {%{$atoms->{$_}}, "symbol" => $_}
    } keys %AtomProperties::atoms;

#* USAGE:
#*    $0 --options abinit*.out > output.cif
#* 
#* OPTIONS:
#*   --input-file-list abinit.files
#*       Give the name of the file that was given as an argument to the Abinit
#*       computation; contains the list of the computation files.
#*
#*   --input-task-descrition
#*       Give the name of the "task description file" for this computation
#*       (the first file mentioned in the --input-file-list file).
#*
#*   --help,--usage         Print a short usage message (this message) and exit.
#**

@ARGV = getOptions( 
    "--input-file-list" => \$input_file_list_name,
    "--input-task-descrition" => \$input_task_description_name,
    "--options" => sub {
        print <<EOF;
$0: the "--options" option is a placeholder where you can put
any options supported by the $0 program. 
Please see the option list below:
EOF

        SUsage::usage; exit
    },
    "--help,--usage" => sub { SUsage::usage; exit },
);

@ARGV = ( "-" ) unless @ARGV;

for my $filename (@ARGV) {
    open( LOG, $filename ) or
        die "could not open '$filename' for reading: ", lcfirst($!);

    my @log_lines;
    my %log_data;

    my %flags;
    my @znumbers;
    my @cell;
    my @cell_angles;

    while(<LOG>) {
        push( @log_lines, $_ );

        if( /^\.Version\s+([\d\.]+)\s+of\s+(ABINIT)/ ) {
            $log_data{program} = $2;
            $log_data{version} = $1;
            %flags = ();
            next;
        }
        if( /^\s*spgroup\s+(\d+)/ ) {
            $log_data{spacegroup} = $1;
            %flags = ();
            next;
        }
        if( /^\s*acell\s+[-+\d\.eE]/ ) {
            @cell = split( " " );
            shift( @cell );
            my $units = pop( @cell );
            if( $units eq "Bohr" ) {
                 # Bohr value taken from http://en.wikipedia.org/wiki/Bohr_radius:
                @cell = map { $_ * 0.529177210 } @cell;
            } else {
                die "unknown units '$units' in file '$filename'";
            }
            %flags = ();
            next;
        }
        if( /^\s*Angles/ ) {
            @cell_angles = split( " " );
            shift( @cell_angles );
            shift( @cell_angles );
            pop( @cell_angles );
            %flags = ();
            next;
        }

        if( /^\s*rprim\s+\d+/ ) {
            my @primitive_vector = split( " " );
            shift( @primitive_vector );
            $log_data{primitive_vectors} = [ \@primitive_vector ];
            $flags{in_rprim} = 1;
            next;
        }

        if( /^\s*znucl\s+[-+\d\.eE]+/ ) {
            @znumbers = split( " " );
            shift( @znumbers );
            $flags{in_znucl} = 1;
            next;
        }

        if( /^\s*typat\s+\d+/ ) {
            my @atom_types = split( " " );
            shift( @atom_types );
            $log_data{atom_types} = \@atom_types;
            $flags{in_typat} = 1;
            next;
        }

        if( /^\s*xangst\s+[-+\d\.eE]+/ ) {
            my @atom_coord = split( " " );
            shift( @atom_coord );
            $log_data{atom_coord} = [ \@atom_coord ];
            $flags{in_xangst} = 1;
            next;
        }

        if( /^(\s+\d+)+\s*$/ ) {
            my @atom_types = split( " "  );
            push( @{$log_data{atom_types}}, @atom_types );
        }
        elsif( /^(\s+[-+\d\.eE]+)+\s*$/ && $flags{in_xangst} ) {
            my @atom_coords = split( " " );
            push( @{$log_data{atom_coord}}, \@atom_coords );
        }
        elsif( /^(\s+[-+\d\.eE]+)+\s*$/ && $flags{in_nucl} ) {
            my @extra_znumbers = split( " " );
            push( @znumbers, @extra_znumbers );
        }
        elsif( /^(\s+[-+\d\.eE]+)+\s*$/ && $flags{in_rprim} ) {
            my @extra_primitive_vectors = split( " " );
            push( @{$log_data{primitive_vectors}}, \@extra_primitive_vectors );
        }
        else {
            %flags = ();
        }

    }

    close( LOG ) or die( "error reading file '$filename': " . lcfirst($!) );

    my $data_name =
        File::Basename::basename( $filename, ('.log', '.dat', '.out'));

    print "data_", $data_name, "\n";

    print "_audit_creation_method '", $Id, "'\n";

    print "_tcod_software_package ", $log_data{program}, "\n"
        if $log_data{program};
    print "_tcod_software_package_version ", $log_data{version}, "\n"
        if $log_data{version};

    $cell[0] *= vlength( $log_data{primitive_vectors}[0] );
    $cell[1] *= vlength( $log_data{primitive_vectors}[1] );
    $cell[2] *= vlength( $log_data{primitive_vectors}[2] );

    print "_cell_length_a ", $cell[0], "\n";
    print "_cell_length_b ", $cell[1], "\n";
    print "_cell_length_c ", $cell[2], "\n";

    print "_cell_angle_alpha ", sprintf( "%f", $cell_angles[0] ), "\n";
    print "_cell_angle_beta  ", sprintf( "%f", $cell_angles[1] ), "\n";
    print "_cell_angle_gamma ", sprintf( "%f", $cell_angles[2] ), "\n";

    print "_space_group_IT_number ", $log_data{spacegroup}, "\n"
        if $log_data{spacegroup};

    if( $log_data{atom_types} ) {
        print "loop_\n";
        print "_atom_site_label\n";
        print "_atom_site_type_symbol\n";
        print "_atom_site_Cartn_x\n";
        print "_atom_site_Cartn_y\n";
        print "_atom_site_Cartn_z\n";
        my $n = 1;
        for my $i (0..$#{$log_data{atom_types}}) {
            my $atomic_number = int($znumbers[$log_data{atom_types}[$i]-1]);
            my $atom = $periodic_table{$atomic_number};
            my $atom_type = $atom->{symbol};
            my $site_label = $atom_type . $n++;
            print $site_label, " ", $atom_type, " ";
            print $log_data{atom_coord}[$i][0], " ";
            print $log_data{atom_coord}[$i][1], " ";
            print $log_data{atom_coord}[$i][2], " ";
            print "\n";
        }
    }

    print "_tcod_input_file\n";
    print "; \\\n";
    map { print " ", $_ } @log_lines;
    print ";\n";

}

sub vlength
{
    my ($vector) = @_;

    return sqrt(
        $vector->[0] ** 2 +
        $vector->[1] ** 2 +
        $vector->[2] ** 2
    );
}
